#[pub]
doc -> Element
  = block

block -> Element // key = mixed
  = blank_line*
    ( block_quote
    / verbatim
    / horizontal_rule
    / heading
    / para
    / plain )

heading -> Element // key = H1..H6
  = setext_heading / atx_heading

atx_inline -> Element // key = Text
  = !newline !(sp "#"* sp newline) xxx_simple_text

atx_start -> Element // key = H1..H6
  = ("######" / "#####" / "####" / "###" / "##" / "#")
    { Element::new(Key::from_heading_level(match_str.len())) }

atx_heading -> Element // key = H1..H6, children = [atx_inline, ..]
  = s:atx_start sp i:atx_inline+ (sp "#"* sp)? newline
    { Element::with_children(s.key, i) }

setext_heading -> Element // key = H1 | H2
  = setext_heading1 / setext_heading2

setext_heading1 -> Element // key = H1, children = [inline, ..]
  = &( raw_line setext_bottom1 )
    a:( !endline inline )+ sp newline
    setext_bottom1
    { Element::with_children(H1, a) }

setext_heading2 -> Element // key = H2, children = [inline, ..]
  = &( raw_line setext_bottom2 )
    a:( !endline inline )+ sp newline
    setext_bottom1
    { Element::with_children(H2, a) }

setext_bottom1 = "="+ newline

setext_bottom2 = "-"+ newline

block_quote -> Element // key = BlockQuote, children = [block_quote_raw]
  = a:block_quote_raw
    { Element::with_children(BlockQuote, vec![a]) }

block_quote_raw -> Element // key = Raw, text = raw Markdown string
  = a:block_quote_raw_chunk+
    { Element::new_text(a.concat().as_slice()).put_key(Raw) }

block_quote_raw_chunk -> String // raw Markdown string
  = a:( ">" " "? line )
    b:( !">" !blank_line line )*
    c:( blank_line { "\n" } )*
    {
        let mut strs = vec![a.text_as_slice()];
        strs.extend(b.iter().map(|e| e.text_as_slice()));
        strs.push_all(c.as_slice());
        strs.concat()
    }

verbatim -> Element // key = Verbatim, text = verbatim chunks
  = a:verbatim_chunk+
    { Element::new_text(a.concat().as_slice()).put_key(Verbatim) }

verbatim_chunk -> String
  = a:blank_line*
    b:nonblank_indented_line+
    {
        let mut v = Vec::from_elem(a.len(), "\n");
        v.extend(b.iter().map(|elt| elt.text_as_slice()));
        v.concat().to_string()
    }

nonblank_indented_line -> Element // key = Text, text = indented_line
  = !blank_line indented_line

horizontal_rule -> Element // key = HorizontalRule
  = nonindent_space
    ( "*" sp "*" sp "*" (sp "*")*
    / "-" sp "-" sp "-" (sp "-")*
    / "_" sp "_" sp "_" (sp "_")* )
    sp newline blank_line
    { Element::new(HorizontalRule) }


para -> Element // key = Para, children = [inline | endline, ..]
  = nonindent_space a:inlines blank_line+
    { a.put_key(Para) }

plain -> Element // key = Plain, children = [inline | endline, ..]
  = a:inlines
    { a.put_key(Plain) }

inlines -> Element // key = List, children = [inline | endline, ..]
  = a:(!endline inline / e:endline &inline { e })+ endline?
    { Element::new_list(a) }

inline -> Element // key = Text | endline-ish, text = simple text | endline-ish
  = xxx_simple_text / endline

xxx_simple_text -> Element // type = Text, text = simple text
  = xxx_simple_str { Element::new_text(match_str) }

xxx_simple_str
  = [a-zA-Z0-9]+

nonindent_space
  = "   " / "  " / " " / ""

endline -> Element // key = LineBreak | Terminal | Space, text = endline-ish
  = line_break / terminal_endline / normal_endline

normal_endline -> Element // key = Space, text = "\n"
  = sp newline !blank_line !">" !atx_start
    !(line ("="+ / "-"+) newline)
    { Element::new_text("\n").put_key(Space) }

terminal_endline -> Element // key = Terminal
  = sp newline eof { Element::new(Terminal) }

line_break -> Element // key = LineBreak
  = "  " normal_endline { Element::new(LineBreak) }

blank_line = sp newline
eof        = !.
sp         = spacechar*
spacechar  = " " / "\t"
newline    = "\n" / "\r" "\n"? { () }

indent = "\t" / "    "
indented_line -> Element // key = Text, text = line
  = indent line

line -> Element // key = Text, text = raw_line
  = a:raw_line { Element::new_text(a.as_slice()) }

raw_line -> String
  = non_breaking_chars_and_newline / a:any_chars eof { a }

non_breaking_chars_and_newline -> String
  = (!"\r" !"\n" .)* newline { match_str.to_string() }

any_chars -> String
  = .+ { match_str.to_string() }
